apply plugin: 'application'
apply plugin: 'kotlin' // implicitly applies java plugin
apply plugin: "com.github.ben-manes.versions"

group = 'moe.giga'
version = '0.8.1'

mainClassName = 'moe.giga.discord.LucoaBot'
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8

buildscript {
    ext.kotlin_version = '1.2.60'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'io.github.cdimascio:java-dotenv:3.1.1'
    compile 'org.tinylog:tinylog:1.3.5'
    compile 'org.tinylog:slf4j-binding:1.3.5'
    compile 'net.dv8tion:JDA:3.7.1_395'
    compile 'com.github.seratch:kotliquery:1.2.0'
    compile 'com.zaxxer:HikariCP:3.2.0'
    compile 'org.postgresql:postgresql:42.2.4'
    compile 'io.github.classgraph:classgraph:4.0.6'
    compile 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.moshi:moshi:1.6.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.6.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': 'moe.giga.discord.LucoaBot'
    }
}

task fatJar(type: Jar) {
    baseName = project.name + '-withDependencies'
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': 'moe.giga.discord.LucoaBot'
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

def updateApplicationProperties() {
    def configFile = new File('src/main/resources/application.properties')
    println "updating version to '${version}' in ${configFile}"
    String configContent = configFile.getText('UTF-8')
    configContent = configContent.replaceAll(/info\.build\.version=.*/, "info.build.version=${version}")
    configFile.write(configContent, 'UTF-8')
}

allprojects {
    updateApplicationProperties()
}

//Creates the w/ dependencies jar.
assemble.dependsOn fatJar