buildscript {
    ext.kotlin_version = '1.3.21'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
    }
}

apply plugin: 'application'
apply plugin: 'kotlin' // implicitly applies java plugin

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.github.ben-manes.versions"

group = 'moe.giga'
version = '0.9.2'

mainClassName = 'moe.giga.discord.LucoaBotKt'
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'io.github.cdimascio:java-dotenv:5.0.1'

    implementation 'org.tinylog:tinylog:1.3.6'
    implementation 'org.tinylog:slf4j-binding:1.3.6'

    implementation 'net.dv8tion:JDA:3.8.3_462'

    implementation 'com.github.seratch:kotliquery:1.3.0'
    implementation 'com.zaxxer:HikariCP:3.3.1'
    implementation 'org.postgresql:postgresql:42.2.5'

    implementation 'io.github.classgraph:classgraph:4.8.22'

    implementation 'com.squareup.okhttp3:okhttp:3.13.0'
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': mainClassName
    }
}

def updateApplicationProperties() {
    def configFile = new File('src/main/resources/application.properties')
    println "updating version to '${version}' in ${configFile}"
    String configContent = configFile.getText('UTF-8')
    configContent = configContent.replaceAll(/info\.build\.version=.*/, "info.build.version=${version}")
    configFile.write(configContent, 'UTF-8')
}

allprojects {
    updateApplicationProperties()
}

//Creates the w/ dependencies jar.
assemble.dependsOn shadowJar