apply plugin: 'application'
apply plugin: 'kotlin' // implicitly applies java plugin
apply plugin: "com.github.ben-manes.versions"

group = 'moe.giga'
version = '0.5.0'

mainClassName = 'moe.giga.discord.LucoaBot'
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8

buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.17.0"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'io.github.cdimascio:java-dotenv:3.1.0'
    compile 'org.tinylog:tinylog:1.3.4'
    compile 'org.tinylog:slf4j-binding:1.3.4'
    compile 'com.google.code.gson:gson:2.8.4'
    compile 'net.dv8tion:JDA:3.6.0_362'
    compile 'org.xerial:sqlite-jdbc:3.21.0.1'
    compile 'org.reflections:reflections:0.9.11'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': 'moe.giga.discord.LucoaBot'
    }
}

task fatJar(type: Jar) {
    baseName = project.name + '-withDependencies'
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': 'moe.giga.discord.LucoaBot'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

def updateApplicationProperties() {
    def configFile = new File('src/main/resources/application.properties')
    println "updating version to '${version}' in ${configFile}"
    String configContent = configFile.getText('UTF-8')
    configContent = configContent.replaceAll(/info\.build\.version=.*/, "info.build.version=${version}")
    configFile.write(configContent, 'UTF-8')
}

allprojects {
    updateApplicationProperties()
}

//Creates the w/ dependencies jar.
assemble.dependsOn fatJar