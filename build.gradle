buildscript {
    ext.kotlin_version = '1.3.0'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
    }
}

apply plugin: 'application'
apply plugin: 'kotlin' // implicitly applies java plugin
apply plugin: "com.github.ben-manes.versions"

group = 'moe.giga'
version = '0.9.0'

mainClassName = 'moe.giga.discord.LucoaBot'
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'io.github.cdimascio:java-dotenv:3.1.3'
    implementation 'org.tinylog:tinylog:1.3.5'
    implementation 'org.tinylog:slf4j-binding:1.3.5'
    implementation 'net.dv8tion:JDA:3.8.1_439'
    implementation 'com.github.seratch:kotliquery:1.2.3'
    implementation 'com.zaxxer:HikariCP:3.2.0'
    implementation 'org.postgresql:postgresql:42.2.5'
    implementation 'io.github.classgraph:classgraph:4.4.12'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.8.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    baseName = project.name + '-withDependencies'
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': mainClassName
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

def updateApplicationProperties() {
    def configFile = new File('src/main/resources/application.properties')
    println "updating version to '${version}' in ${configFile}"
    String configContent = configFile.getText('UTF-8')
    configContent = configContent.replaceAll(/info\.build\.version=.*/, "info.build.version=${version}")
    configFile.write(configContent, 'UTF-8')
}

allprojects {
    updateApplicationProperties()
}

//Creates the w/ dependencies jar.
assemble.dependsOn fatJar